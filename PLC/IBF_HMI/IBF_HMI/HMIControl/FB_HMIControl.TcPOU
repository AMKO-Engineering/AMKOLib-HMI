<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.11">
  <POU Name="FB_HMIControl" Id="{062d2a70-ec77-4a9b-a458-d823fff51efb}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_HMIControl
VAR_IN_OUT
	fbLogHandler_PersistentArray : FB_LogHandler_PersistentArray;
END_VAR
VAR_INPUT
	eScreenRequest 			: E_BaseVisu;
	eHomeScreen   			: E_BaseVisu := E_BaseVisu.Idle; // Home screen is based on system state, e.g., Idle, Automatic, etc.

	bShowEmergencyOverlay 	: BOOL;
	sEmergencyText 			: STRING; // Visible when emergency overlay is shown
	sStatusText 			: STRING; // Text in the bottom border of the screen 
END_VAR
VAR_OUTPUT
	eShownScreen : E_BaseVisu := E_BaseVisu.Idle;

	// Screen states
	bInScreenHome 		: BOOL;
	bInScreenSettings 	: BOOL;
	bInScreenManual 	: BOOL;
	bInScreenParameters	: BOOL;
	bInScreenLog		: BOOL;
	
	// Hide admin/service level navigation buttons
	bHideSecureButtons : BOOL;
END_VAR
VAR
	fbLogView			: FB_LogView;

	feWriteParameter 	: F_TRIG ; // Falling edge trigger on leaving parameter or manual screen
	bWritePar  			: BOOL ; // Busy writing parameters
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[// Run log view handler
fbLogView(fbLogHandler_PersistentArray := fbLogHandler_PersistentArray);

// Check user level
bHideSecureButtons := NOT (VisuElems.CurrentUserGroupName = "Admin" OR VisuElems.CurrentUserGroupName = "Service");

// Check if logged in user is allowed to be in the current screen. If not, go to home screen.
IF bHideSecureButtons AND ((eShownScreen = E_BaseVisu.Manual) OR (eShownScreen = E_BaseVisu.Parameter)) THEN
	eScreenRequest := E_BaseVisu.Idle;
END_IF 

// Switch screen
IF (eScreenRequest = E_BaseVisu.Idle) THEN
	eShownScreen := eHomeScreen;
ELSIF (eScreenRequest <> eShownScreen) THEN
	eShownScreen := eScreenRequest;
END_IF

// Report screen states
bInScreenHome 		:= (eShownScreen = eHomeScreen); 
bInScreenSettings 	:= (eShownScreen = E_BaseVisu.Settings);
bInScreenManual 	:= (eShownScreen = E_BaseVisu.Manual);
bInScreenLog 		:= (eShownScreen = E_BaseVisu.LogView);
bInScreenParameters := (eShownScreen = E_BaseVisu.Parameter);

// Save parameters after leaving parameter of manual screen
// Note: in manual operation one can change parameters as well ("learn" function)
feWriteParameter(CLK := (eShownScreen = E_BaseVisu.Parameter) OR (eShownScreen = E_BaseVisu.Manual));
IF feWriteParameter.Q THEN
	bWritePar := TRUE;
END_IF
IF bWritePar THEN 
	bWritePar R= GVL_Parameters.fbParameterHandler.CmdSavePar();
END_IF
]]></ST>
    </Implementation>
  </POU>
</TcPlcObject>